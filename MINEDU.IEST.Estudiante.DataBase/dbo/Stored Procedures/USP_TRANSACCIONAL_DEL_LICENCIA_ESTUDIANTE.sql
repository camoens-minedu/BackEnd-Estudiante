/**********************************************************************************************************
AUTOR				:	Mayra Alva
FECHA DE CREACION	:	20/06/2019
LLAMADO POR			:
DESCRIPCION			:	Elimina el registro de licencia del estudiante.
REVISIONES			:
-----------------------------------------------------------------------------------------------------------
VERSIÓN		FECHA MODIF.	USUARIO			DESCRIPCIÓN
-----------------------------------------------------------------------------------------------------------
1.0			20/06/2019		MALVA			CREACIÓN
1.1			31/01/2020		MALVA			SE AÑADE PARÁMETRO @ID_INSTITUCION PARA VERIFICAR SI ESTÁ PERMITIDO ELIMINAR REGISTRO. 
1.2			07/02/2022		JCHAVEZ			SE MODIFICÓ VALIDACIÓN -329	PARA VALIDAR PERIODO DE CLASES Y NO CLASES CERRADAS

  TEST:		USP_TRANSACCIONAL_DEL_LICENCIA_ESTUDIANTE 1106, 1040, 'MALVA'
**********************************************************************************************************/
CREATE PROCEDURE [dbo].[USP_TRANSACCIONAL_DEL_LICENCIA_ESTUDIANTE]
(
	@ID_INSTITUCION					INT, 
    @ID_LICENCIA_ESTUDIANTE	        INT,        
    @USUARIO						nvarchar(20)
)
AS
BEGIN
SET NOCOUNT ON;
	DECLARE @RESULT INT, @ID_INSTITUCION_CONSULTA INT = 0, @ID_ESTUDIANTE_INSTITUTO INT, @ID_PERIODOS_LECTIVOS_INSTITUCION INT, @ID_MATRICULA_ESTUDIANTE INT, @ID_PROGRAMACION_CLASE INT, @ID_PERIODO_ACADEMICO INT

	SELECT  
	@ID_INSTITUCION_CONSULTA = plxi.ID_INSTITUCION,
	@ID_PERIODOS_LECTIVOS_INSTITUCION = le.ID_PERIODOS_LECTIVOS_POR_INSTITUCION FROM transaccional.licencia_estudiante le
	INNER JOIN transaccional.periodos_lectivos_por_institucion plxi ON plxi.ID_PERIODOS_LECTIVOS_POR_INSTITUCION = le.ID_PERIODOS_LECTIVOS_POR_INSTITUCION 
	AND le.ES_ACTIVO=1 AND plxi.ES_ACTIVO=1
	WHERE ID_LICENCIA_ESTUDIANTE = @ID_LICENCIA_ESTUDIANTE 

	IF @ID_INSTITUCION <> @ID_INSTITUCION_CONSULTA
	BEGIN 
		SET @RESULT = -362 GOTO FIN
	END

	SET @ID_ESTUDIANTE_INSTITUTO = (SELECT TOP 1 ID_ESTUDIANTE_INSTITUCION FROM transaccional.licencia_estudiante WHERE ID_LICENCIA_ESTUDIANTE = @ID_LICENCIA_ESTUDIANTE AND ES_ACTIVO = 1)
	SET @ID_MATRICULA_ESTUDIANTE = (SELECT TOP 1 ID_MATRICULA_ESTUDIANTE FROM transaccional.matricula_estudiante WHERE ID_ESTUDIANTE_INSTITUCION = @ID_ESTUDIANTE_INSTITUTO 
									AND ID_PERIODOS_LECTIVOS_POR_INSTITUCION = @ID_PERIODOS_LECTIVOS_INSTITUCION AND ES_ACTIVO = 1)
	SET @ID_PROGRAMACION_CLASE = (SELECT TOP 1 ID_PROGRAMACION_CLASE FROM transaccional.programacion_clase_por_matricula_estudiante WHERE ID_MATRICULA_ESTUDIANTE = @ID_MATRICULA_ESTUDIANTE 
									AND ES_ACTIVO = 1)
	SET @ID_PERIODO_ACADEMICO = (SELECT TOP 1 ID_PERIODO_ACADEMICO FROM transaccional.programacion_clase WHERE ID_PROGRAMACION_CLASE = @ID_PROGRAMACION_CLASE AND ES_ACTIVO = 1)

	--IF EXISTS (SELECT TOP 1 ID_EVALUACION FROM transaccional.evaluacion WHERE ID_PROGRAMACION_CLASE = @ID_PROGRAMACION_CLASE AND CIERRE_PROGRAMACION = 235 AND ES_ACTIVO = 1)
	IF EXISTS(SELECT TOP 1 ID_PROGRAMACION_CLASE FROM transaccional.programacion_clase WHERE ID_PERIODO_ACADEMICO = @ID_PERIODO_ACADEMICO AND ESTADO = 0 AND ES_ACTIVO = 1)
		SET @RESULT = -329
	ELSE IF EXISTS (SELECT TOP 1 ID_REINGRESO_ESTUDIANTE FROM transaccional.reingreso_estudiante WHERE ID_LICENCIA_ESTUDIANTE = @ID_LICENCIA_ESTUDIANTE AND ES_ACTIVO = 1)
		SET @RESULT = -162
	ELSE
	BEGIN
		 UPDATE transaccional.licencia_estudiante
		   SET 
			   [ES_ACTIVO]	= 0 
			  ,[FECHA_MODIFICACION]	  = GETDATE()   
			  ,[USUARIO_MODIFICACION]	  = @USUARIO
		 WHERE 
			   [ID_LICENCIA_ESTUDIANTE]			  = @ID_LICENCIA_ESTUDIANTE		 
			   
		 SET @RESULT = 1
	END
	FIN:
	SELECT @RESULT
END
GO


