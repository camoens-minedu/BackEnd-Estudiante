--/***************************************************************************************************************************************
--AUTOR				:	Juan Tovar
--FECHA DE CREACION	:	20/06/2019
--LLAMADO POR			:
--DESCRIPCION			:	Actualización de registro de postulante
--REVISIONES			:
-------------------------------------------------------------------------------------------------------------
--VERSIÓN		FECHA MODIF.	USUARIO			DESCRIPCIÓN
-------------------------------------------------------------------------------------------------------------
----  TEST:		
--/*
--	1.0			08/01/2020		MALVA          MODIFICACIÓN, SE CONSIDERA EL PARÁMETRO @ES_DISCAPACITADO PARA LA ACTUALIZACIÓN DE TABLA PERSONA. 
--	1.1			07/02/2020		MALVA          MODIFICACIÓN, EN LA INSERCIÓN O MODIFICACIÓN DE INSTITUCIÓN BÁSICA DE TIPO EBA
--											   SE CONSIDERA QUE NO TRAE CÓDIGO MODULAR. 
--*/
--****************************************************************************************************************************************/
CREATE PROCEDURE [dbo].[USP_ADMISION_UPD_POSTULANTE_MODALIDAD]
(
	@ID_PERSONA						INT,
	@ID_PERSONA_INSTITUCION			INT,
	@ID_POSTULANTE_MODALIDAD		INT,
	
	@NOMBRE_PERSONA					VARCHAR(50),
	@APELLIDO_PATERNO_PERSONA		VARCHAR(40),
	@APELLIDO_MATERNO_PERSONA		VARCHAR(40),
	@FECHA_NACIMIENTO_PERSONA		DATETIME,
	@SEXO_PERSONA					INT,
	@ID_LENGUA_MATERNA				INT,
	@ES_DISCAPACITADO				BIT,
	@PAIS_NACIMIENTO				INT,
	@UBIGEO_NACIMIENTO				VARCHAR(6),

	@ID_TIPO_DOCUMENTO_APODERADO	INT,
	@NUMERO_DOCUMENTO_APODERADO		VARCHAR(16),
	@NOMBRE_APODERADO				VARCHAR(50),
	@APELLIDO_APODERADO				VARCHAR(150),
	@ID_TIPO_PARENTEZCO				INT,

	@ESTADO_CIVIL					INT,
	@PAIS_PERSONA					INT,
	@UBIGEO_PERSONA					VARCHAR(6),
	@DIRECCION_PERSONA				VARCHAR(255),
	@TELEFONO						VARCHAR(15),
	@CELULAR						VARCHAR(15),
	@CORREO							VARCHAR(100),
	@ID_TIPO_DISCAPACIDAD			INT,

	@ID_INSTITUCION_BASICA			INT,
	@ANIO_EGRESO					INT,
	@CODIGO_POSTULANTE				VARCHAR(16),
	@CODIGO_ESTUDIANTE				VARCHAR(16),
	@ID_TIPO_INSTITUCION_BASICA		INT,
	@CODIGO_MODULAR					VARCHAR(7),
	@NOMBRE_IE_BASICA				VARCHAR(150),
	@ID_NIVEL_IE_BASICA				INT,
	@ID_TIPO_GESTION_IE_BASICA		INT,
	@DIRECCION_IE_BASICA			VARCHAR(255),
	@ID_PAIS_BASICA					INT,
	@UBIGEO_IE_BASICA				VARCHAR(6),

	@ARCHIVO_FOTO					VARCHAR(50),
	@ARCHIVO_RUTA					VARCHAR(255),
	@ID_MODALIDAD_PROCESO_ADMISION	INT,
	@ID_TIPO_MODALIDAD_INSTITUCION	INT,
	@ID_EXAMEN_ADMISION_SEDE		INT,
	@OPCIONES						VARCHAR(MAX),

	@ID_TIPO_PAGO					INT,
	@NUMERO_COMPROBANTE				VARCHAR(16),
	@MONTO_PAGO						DECIMAL(10,2),
	@MOTIVO_EXONERACION				VARCHAR(255),

	@REQUISITOS						VARCHAR(MAX),
	
	@USUARIO						VARCHAR(20)
)
AS
BEGIN
	DECLARE @RESULT INT
	
	DECLARE @FECHA_ACTUAL DATE 
	DECLARE @ESTADO_CIVIL_UPDATE INT
	DECLARE @TIPO_DOCUMENTO INT
	
	SET @FECHA_ACTUAL = cast(getdate() as date )

--En caso el tipo de documento sea DNI no debe permitirse modificar el valor ya que los datos se obtienen de la validación de RENIEC

SET @TIPO_DOCUMENTO = (SELECT ID_TIPO_DOCUMENTO FROM maestro.persona WHERE ID_PERSONA = @ID_PERSONA)

IF @TIPO_DOCUMENTO = 26
BEGIN
	SET @ESTADO_CIVIL_UPDATE = (SELECT ESTADO_CIVIL FROM maestro.persona_institucion WHERE ID_PERSONA_INSTITUCION = @ID_PERSONA_INSTITUCION)
END
ELSE
BEGIN
	SET @ESTADO_CIVIL_UPDATE = @ESTADO_CIVIL
END

	IF EXISTS (select TOP 1 ID_MODALIDADES_POR_PROCESO_ADMISION 
	from transaccional.modalidades_por_proceso_admision where ID_MODALIDADES_POR_PROCESO_ADMISION=@ID_MODALIDAD_PROCESO_ADMISION
		AND @FECHA_ACTUAL>FECHA_FIN_REGISTRO)
	SET @RESULT = -285
	ELSE
	BEGIN
	BEGIN TRY
	BEGIN TRANSACTION T1	
		DECLARE @REGISTRO_EXISTE BIT = 0, @ID_INSTITUCION_BASICA_PREVIO INT 
						
			IF  @ID_TIPO_INSTITUCION_BASICA = 70   -- IE BÁSICA EBR 
			AND EXISTS (SELECT TOP 1 ID_INSTITUCION_BASICA FROM maestro.institucion_basica (NOLOCK)
									WHERE ID_TIPO_INSTITUCION_BASICA = @ID_TIPO_INSTITUCION_BASICA AND CODIGO_MODULAR_IE_BASICA = @CODIGO_MODULAR
									AND ID_PAIS = @ID_PAIS_BASICA AND ID_NIVEL_IE_BASICA=@ID_NIVEL_IE_BASICA 
									AND ID_TIPO_GESTION_IE_BASICA= @ID_TIPO_GESTION_IE_BASICA)
									SET @REGISTRO_EXISTE =1
			
		IF @ID_INSTITUCION_BASICA = 0
		BEGIN
			--Eliminar el registro previo (eba)
				SET @ID_INSTITUCION_BASICA_PREVIO = (SELECT TOP 1 ID_INSTITUCION_BASICA FROM transaccional.postulantes_por_modalidad WHERE ID_POSTULANTES_POR_MODALIDAD = @ID_POSTULANTE_MODALIDAD AND ES_ACTIVO=1)
				UPDATE maestro.institucion_basica set ESTADO=0, USUARIO_MODIFICACION=@USUARIO, FECHA_MODIFICACION= GETDATE()  where ID_INSTITUCION_BASICA= @ID_INSTITUCION_BASICA_PREVIO and ID_TIPO_INSTITUCION_BASICA<>70
			IF @REGISTRO_EXISTE = 0
			BEGIN
				INSERT INTO maestro.institucion_basica(
					ID_TIPO_INSTITUCION_BASICA,
					CODIGO_MODULAR_IE_BASICA,
					NOMBRE_IE_BASICA,
					ID_NIVEL_IE_BASICA,
					ID_TIPO_GESTION_IE_BASICA,
					DIRECCION_IE_BASICA,
					ID_PAIS,
					UBIGEO_IE_BASICA,
					ESTADO,
					USUARIO_CREACION,
					FECHA_CREACION
				)VALUES(
					@ID_TIPO_INSTITUCION_BASICA,
					@CODIGO_MODULAR,
					@NOMBRE_IE_BASICA,
					@ID_NIVEL_IE_BASICA,
					@ID_TIPO_GESTION_IE_BASICA,
					@DIRECCION_IE_BASICA,
					@ID_PAIS_BASICA,
					RIGHT(REPLICATE('0',6) + @UBIGEO_IE_BASICA,6),
					1,
					@USUARIO,
					GETDATE()
				)

				SET @ID_INSTITUCION_BASICA = CONVERT(INT,@@IDENTITY)
			END
			ELSE
			BEGIN
				SET @ID_INSTITUCION_BASICA = (SELECT TOP 1 ID_INSTITUCION_BASICA FROM maestro.institucion_basica (NOLOCK)
					WHERE ID_TIPO_INSTITUCION_BASICA = @ID_TIPO_INSTITUCION_BASICA AND CODIGO_MODULAR_IE_BASICA = @CODIGO_MODULAR
					AND ID_PAIS = @ID_PAIS_BASICA AND ID_NIVEL_IE_BASICA=@ID_NIVEL_IE_BASICA 
					AND ID_TIPO_GESTION_IE_BASICA= @ID_TIPO_GESTION_IE_BASICA)

				UPDATE maestro.institucion_basica
				SET NOMBRE_IE_BASICA = @NOMBRE_IE_BASICA,
					ID_NIVEL_IE_BASICA = @ID_NIVEL_IE_BASICA,
					ID_TIPO_GESTION_IE_BASICA = @ID_TIPO_GESTION_IE_BASICA,
					DIRECCION_IE_BASICA = @DIRECCION_IE_BASICA,
					UBIGEO_IE_BASICA = RIGHT(REPLICATE('0',6) + @UBIGEO_IE_BASICA,6),
					USUARIO_MODIFICACION = @USUARIO,
					FECHA_MODIFICACION = GETDATE()
				WHERE 
					ID_INSTITUCION_BASICA = @ID_INSTITUCION_BASICA
			END
		END
		ELSE
		BEGIN
			UPDATE maestro.institucion_basica
			SET ID_TIPO_INSTITUCION_BASICA = @ID_TIPO_INSTITUCION_BASICA,
				CODIGO_MODULAR_IE_BASICA = @CODIGO_MODULAR,
				NOMBRE_IE_BASICA = @NOMBRE_IE_BASICA,
				ID_NIVEL_IE_BASICA = @ID_NIVEL_IE_BASICA,
				ID_TIPO_GESTION_IE_BASICA = @ID_TIPO_GESTION_IE_BASICA,
				DIRECCION_IE_BASICA = @DIRECCION_IE_BASICA,
				UBIGEO_IE_BASICA = RIGHT(REPLICATE('0',6) + @UBIGEO_IE_BASICA,6),
				USUARIO_MODIFICACION = @USUARIO,
				FECHA_MODIFICACION = GETDATE()
			WHERE 
				ID_INSTITUCION_BASICA = @ID_INSTITUCION_BASICA	
		END		
		UPDATE maestro.persona
		SET ID_LENGUA_MATERNA = @ID_LENGUA_MATERNA,
		FECHA_NACIMIENTO_PERSONA= @FECHA_NACIMIENTO_PERSONA, 
		ES_DISCAPACITADO = @ES_DISCAPACITADO,
		USUARIO_MODIFICACION = @USUARIO,
		FECHA_MODIFICACION = GETDATE()
		WHERE ID_PERSONA=@ID_PERSONA

		UPDATE maestro.persona_institucion
		SET 
		    --ESTADO_CIVIL = @ESTADO_CIVIL,
			ESTADO_CIVIL = @ESTADO_CIVIL_UPDATE,
			PAIS_PERSONA = @PAIS_PERSONA,
			UBIGEO_PERSONA = RIGHT(REPLICATE('0',6) + @UBIGEO_PERSONA,6),
			DIRECCION_PERSONA = @DIRECCION_PERSONA,
			TELEFONO = @TELEFONO,
			CELULAR = @CELULAR,
			CORREO = @CORREO,	
			ID_TIPO_DISCAPACIDAD = @ID_TIPO_DISCAPACIDAD,
			ESTADO = 1,
			USUARIO_MODIFICACION = @USUARIO,
			FECHA_MODIFICACION = GETDATE()
		WHERE ID_PERSONA_INSTITUCION = @ID_PERSONA_INSTITUCION
		UPDATE transaccional.postulantes_por_modalidad
		SET ID_MODALIDADES_POR_PROCESO_ADMISION = @ID_MODALIDAD_PROCESO_ADMISION,
			ID_TIPOS_MODALIDAD_POR_INSTITUCION = @ID_TIPO_MODALIDAD_INSTITUCION,
			ID_EXAMEN_ADMISION_SEDE = @ID_EXAMEN_ADMISION_SEDE,
			ID_INSTITUCION_BASICA = @ID_INSTITUCION_BASICA,
			ANIO_EGRESO = @ANIO_EGRESO,
			ID_TIPO_DOCUMENTO_APODERADO = @ID_TIPO_DOCUMENTO_APODERADO,
			NUMERO_DOCUMENTO_APODERADO = @NUMERO_DOCUMENTO_APODERADO,
			NOMBRE_APODERADO = @NOMBRE_APODERADO,
			APELLIDO_APODERADO = @APELLIDO_APODERADO,
			ID_TIPO_PARENTEZCO = @ID_TIPO_PARENTEZCO,
			ARCHIVO_FOTO = @ARCHIVO_FOTO,
			ARCHIVO_RUTA = @ARCHIVO_RUTA,
			ID_TIPO_PAGO = @ID_TIPO_PAGO,
			NUMERO_COMPROBANTE = @NUMERO_COMPROBANTE,
			MONTO_PAGO = @MONTO_PAGO,
			MOTIVO_EXONERACION = @MOTIVO_EXONERACION,
			USUARIO_MODIFICACION = @USUARIO,
			FECHA_MODIFICACION = GETDATE()
		WHERE ID_POSTULANTES_POR_MODALIDAD = @ID_POSTULANTE_MODALIDAD

		--OPCIONES
		MERGE INTO transaccional.opciones_por_postulante AS TARGET
		USING (
			SELECT	@ID_POSTULANTE_MODALIDAD ID_POSTULANTES_POR_MODALIDAD,
					CONVERT(INT,SUBSTRING(SplitData,0,CHARINDEX(':',SplitData))) ID_META_CARRERA_INSTITUCION_DETALLE,
					CONVERT(INT,SUBSTRING(SplitData,CHARINDEX(':',SplitData)+1,LEN(SplitData))) ORDEN
			FROM dbo.UFN_SPLIT(UPPER(@OPCIONES), '|')				
		) AS SOURCE ON TARGET.ID_POSTULANTES_POR_MODALIDAD = SOURCE.ID_POSTULANTES_POR_MODALIDAD
			AND TARGET.ID_META_CARRERA_INSTITUCION_DETALLE = SOURCE.ID_META_CARRERA_INSTITUCION_DETALLE	
		WHEN MATCHED
		THEN
			UPDATE 
			SET ES_ACTIVO = 1,
				ORDEN = SOURCE.ORDEN,
				USUARIO_MODIFICACION = @USUARIO,		
				FECHA_MODIFICACION = GETDATE()
		WHEN NOT MATCHED BY TARGET
		THEN
			INSERT (
				ID_POSTULANTES_POR_MODALIDAD,
				ID_META_CARRERA_INSTITUCION_DETALLE,
				ORDEN,
				ES_ACTIVO,
				ESTADO,
				USUARIO_CREACION,
				FECHA_CREACION
			) VALUES (
				SOURCE.ID_POSTULANTES_POR_MODALIDAD,
				SOURCE.ID_META_CARRERA_INSTITUCION_DETALLE,
				SOURCE.ORDEN,
				1,
				1,
				@USUARIO,
				GETDATE()
			)
		WHEN NOT MATCHED BY SOURCE AND TARGET.ID_POSTULANTES_POR_MODALIDAD = @ID_POSTULANTE_MODALIDAD
		THEN
			UPDATE 
			SET ES_ACTIVO = 0,
				USUARIO_MODIFICACION = @USUARIO,		
				FECHA_MODIFICACION = GETDATE();
		--REQUISITOS
		MERGE INTO transaccional.requisitos_por_postulante AS TARGET
		USING (
			SELECT @ID_POSTULANTE_MODALIDAD ID_POSTULANTES_POR_MODALIDAD,SplitData ID_REQUISITOS_POR_TIPO_MODALIDAD 
			FROM dbo.UFN_SPLIT(UPPER(@REQUISITOS), '|')				
		) AS SOURCE ON TARGET.ID_POSTULANTES_POR_MODALIDAD = SOURCE.ID_POSTULANTES_POR_MODALIDAD
			AND TARGET.ID_REQUISITOS_POR_TIPO_MODALIDAD = SOURCE.ID_REQUISITOS_POR_TIPO_MODALIDAD	
		WHEN MATCHED
		THEN
			UPDATE 
			SET ES_ACTIVO = 1,
				USUARIO_MODIFICACION = @USUARIO,		
				FECHA_MODIFICACION = GETDATE()
		WHEN NOT MATCHED BY TARGET
		THEN
			INSERT (
				ID_POSTULANTES_POR_MODALIDAD,
				ID_REQUISITOS_POR_TIPO_MODALIDAD,
				ES_ACTIVO,
				ESTADO,
				USUARIO_CREACION,
				FECHA_CREACION
			) VALUES (
				SOURCE.ID_POSTULANTES_POR_MODALIDAD,
				SOURCE.ID_REQUISITOS_POR_TIPO_MODALIDAD,
				1,
				1,
				@USUARIO,
				GETDATE()
			)
		WHEN NOT MATCHED BY SOURCE AND TARGET.ID_POSTULANTES_POR_MODALIDAD = @ID_POSTULANTE_MODALIDAD
		THEN
			UPDATE 
			SET ES_ACTIVO = 0,
				USUARIO_MODIFICACION = @USUARIO,		
				FECHA_MODIFICACION = GETDATE();
	COMMIT TRANSACTION T1		
		SET @RESULT = 1
	END TRY
	BEGIN CATCH	
		IF @@ERROR <> 0
		BEGIN
			ROLLBACK TRANSACTION T1	   
			SET @RESULT = -1
			print ERROR_MESSAGE()
		END
		ELSE
			ROLLBACK TRANSACTION T1	   
	END CATCH
	END
	SELECT @RESULT
	END
GO


