/**********************************************************************************************************
AUTOR				:	Juan Tovar
FECHA DE CREACION	:	17/02/2022
LLAMADO POR			:
DESCRIPCION			:	Actualiza el registro de nota de la evaluacion extraordinaria
REVISIONES			:
-----------------------------------------------------------------------------------------------------------
VERSIÓN		FECHA MODIF.	USUARIO			DESCRIPCIÓN
-----------------------------------------------------------------------------------------------------------

TEST:

**********************************************************************************************************/
CREATE PROCEDURE [dbo].[USP_EVALUACION_UPD_NOTA_UNIDAD_DIDACTICA]
(
	
	@ID_EVALUACION_EXTRAORDINARIA_DETALLE INT,
	@ID_EVALUACION_EXTRAORDINARIA INT,
	@ID_UNIDAD_DIDACTICA INT,
    @NOTA INT,
	@USUARIO VARCHAR(20)
)
AS
BEGIN	
	DECLARE @RESULT INT, @ID_PERIODOS_LECTIVOS_INSTITUCION INT
	SET @ID_PERIODOS_LECTIVOS_INSTITUCION = (SELECT ID_PERIODOS_LECTIVOS_POR_INSTITUCION FROM transaccional.evaluacion_extraordinaria WHERE ID_EVALUACION_EXTRAORDINARIA = @ID_EVALUACION_EXTRAORDINARIA AND ES_ACTIVO = 1)

	IF EXISTS(	SELECT TOP 1 ID_EVALUACION_EXTRAORDINARIA_DETALLE 
				FROM [transaccional].[evaluacion_extraordinaria_detalle] 
				WHERE ID_EVALUACION_EXTRAORDINARIA = @ID_EVALUACION_EXTRAORDINARIA 
				AND ID_UNIDAD_DIDACTICA = @ID_UNIDAD_DIDACTICA AND ES_ACTIVO=1 
				AND ID_EVALUACION_EXTRAORDINARIA_DETALLE <> @ID_EVALUACION_EXTRAORDINARIA_DETALLE
			)
	BEGIN
		SET @RESULT = -180 -- YA SE ENCUENTRA REGISTRADO
	END
	ELSE IF EXISTS(SELECT TOP 1 pc.ID_PROGRAMACION_CLASE
					FROM transaccional.periodo_academico pa
					INNER JOIN transaccional.programacion_clase pc ON pc.ID_PERIODO_ACADEMICO = pa.ID_PERIODO_ACADEMICO AND pc.ES_ACTIVO = 1
					WHERE pa.ID_PERIODOS_LECTIVOS_POR_INSTITUCION=@ID_PERIODOS_LECTIVOS_INSTITUCION AND pa.ES_ACTIVO = 1 AND pc.ESTADO = 0)
	BEGIN
		SET @RESULT = -332 -- PERIODO SE ENCUENTRA CERRADO
    END
	ELSE
	BEGIN
		BEGIN TRY
			BEGIN TRANSACTION T1	

			UPDATE transaccional.evaluacion_extraordinaria_detalle
			SET NOTA = @NOTA, ES_ACTIVO=1, USUARIO_MODIFICACION = @USUARIO, FECHA_MODIFICACION = GETDATE()
			WHERE ID_EVALUACION_EXTRAORDINARIA_DETALLE = @ID_EVALUACION_EXTRAORDINARIA_DETALLE AND 
			ID_EVALUACION_EXTRAORDINARIA = @ID_EVALUACION_EXTRAORDINARIA AND 
			ID_UNIDAD_DIDACTICA = @ID_UNIDAD_DIDACTICA 

			COMMIT TRANSACTION T1		
			SET @RESULT = 1
		END TRY
		BEGIN CATCH	
			IF @@ERROR <> 0
			BEGIN
				ROLLBACK TRANSACTION T1	   
				SET @RESULT = -1
			END
			ELSE
				ROLLBACK TRANSACTION T1	   
		END CATCH
	END

	SELECT @RESULT
END